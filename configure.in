dnl Process this file with autoconf to produce a configure script.
dnl Written in 2000 by Christian Bauer <Christian.Bauer@uni-mainz.de>

AC_INIT(Source_Files/Misc/shell_sdl.cpp)
AC_PREREQ(2.13)

dnl This defines PACKAGE and VERSION.
AM_INIT_AUTOMAKE([AlephOne], [0.10.6])
AM_CONFIG_HEADER(config.h)

dnl Configure options.
AC_ARG_ENABLE(opengl, [  --enable-opengl         use OpenGL for rendering [default=yes]], , enable_opengl=yes)

dnl Detect the canonical host and target build environment.
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Figure out which OpenGL library to use
case "$target" in
  *-*-cygwin* | *-*-mingw32*)
    SYS_GL_LIBS="-lopengl32"
    ;;
  *-*-beos*)
    SYS_GL_LIBS="-lGL"
    ;;
  *)
    AC_PATH_X
    AC_PATH_XTRA
    if test x$have_x = xyes; then
      CFLAGS="$CFLAGS $X_CFLAGS"
      SYS_GL_LIBS="$X_LIBS -lGL"
    else
      SYS_GL_LIBS="-lGL"
    fi
    ;;
esac

dnl Check for SDL.
AM_PATH_SDL(1.1.4, , AC_MSG_ERROR([You need SDL to run Aleph One.]))
CFLAGS="$CFLAGS $SDL_CFLAGS -DSDL"
CXXFLAGS="$CXXFLAGS $SDL_CFLAGS -DSDL"
LIBS="$LIBS $SDL_LIBS"

dnl Checks for headers.
AC_CHECK_HEADERS(unistd.h)

dnl Check for OpenGL.
if [[ "x$enable_opengl" = "xyes" ]]; then
  AC_MSG_CHECKING(for OpenGL support)
  have_opengl=no
  AC_TRY_COMPILE([
   #include <GL/gl.h>
  ],[
  ],[
  have_opengl=yes
  ])
  AC_MSG_RESULT($have_opengl)
  if test x$have_opengl = xyes; then
    LIBS="$LIBS $SYS_GL_LIBS"
    AC_DEFINE(HAVE_OPENGL)
    AC_CHECK_LIB(GLU, gluScaleImage)
  else
    AC_MSG_WARN([Could not find OpenGL, ignoring --enable-opengl.])
  fi
fi

dnl Generate Makefiles.
AC_OUTPUT([
Makefile
AlephOne.spec
Source_Files/Makefile
Source_Files/CSeries/Makefile
Source_Files/Expat/Makefile
Source_Files/Misc/Makefile
Source_Files/Network/Makefile
Source_Files/Pfhortran/Makefile
tools/Makefile
data/Makefile
data/default_theme/Makefile
])

dnl Print summary.
echo "Configuration done. Now type \"make\"."
