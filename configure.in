dnl Process this file with autoconf to produce a configure script.
dnl Written in 2000 by Christian Bauer <Christian.Bauer@uni-mainz.de>

AC_INIT(Source_Files/Misc/shell_sdl.cpp)
AC_PREREQ(2.13)

dnl This defines PACKAGE and VERSION.
AM_INIT_AUTOMAKE([AlephOne], [0.10.2])
AM_CONFIG_HEADER(config.h)

dnl Configure options.
AC_ARG_ENABLE(opengl, [  --enable-opengl         use OpenGL for rendering [default=no]], , enable_opengl=false)

dnl Detect the canonical host and target build environment.
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AC_PROG_CXX
AC_PROG_CXXCPP
AC_PROG_INSTALL
AC_PROG_RANLIB

dnl Check for SDL.
if [[ "x$target" = xmipsel-sony-ecoff ]]; then
  CFLAGS="$CFLAGS `sdl-config --cflags`"
  CXXFLAGS="$CXXFLAGS `sdl-config --cflags`"
else
  AM_PATH_SDL(1.1.4, , AC_MSG_ERROR([You need SDL to run Aleph One.]))
fi
CFLAGS="$CFLAGS $SDL_CFLAGS -DSDL"
CXXFLAGS="$CXXFLAGS $SDL_CFLAGS -DSDL"
LIBS="$LIBS $SDL_LIBS"

dnl Checks for headers.
AC_CHECK_HEADERS(unistd.h)

dnl OpenGL support.
if [[ "x$enable_opengl" = "xyes" ]]; then
  AC_CHECK_LIB(GL, glBegin, [
    LIBS="$LIBS -lGL"
    AC_DEFINE(HAVE_OPENGL)
  ], [
    AC_MSG_WARN([Could not find OpenGL, ignoring --enable-opengl.])
  ])
fi

dnl Generate Makefiles.
AC_OUTPUT([
Makefile
Source_Files/Makefile
Source_Files/CSeries/Makefile
Source_Files/Expat/Makefile
Source_Files/Misc/Makefile
Source_Files/Network/Makefile
Source_Files/Pfhortran/Makefile
tools/Makefile
data/Makefile
])

dnl Print summary.
echo "Configuration done. Now type \"make\"."
